* ----------------------------------------------------------------------  
* PGM.....: MCIN5837                                                      
* MAPAS...: MCIM837C / MCIM835D / MCIM835E                                
* TITULO..: MANTER TIPOS DE SINAL DE ALERTA                               
* AUTOR...: C1196814 - RENATO COELHO                                      
* ANALISTA: C1196814 - RENATO COELHO                                      
* OBJETIVO: MANTER DADOS TIPO ESTADO ALTERACAO A VALIDAR                  
*           DB2MCI.TIP_EST_ALT_VLDR                                       
* ---------------------------------------------------------------------*  
* VRS0001 28/07/2017 C1196814 - IMPLANTACAO ROTINAS NANUNTENÇÃO TIPO      
*                               ESTADO - DE: 147710 AC:185670             
*                               OF:540993                                 
* ---------------------------------------------------------------------*  
*                                                                         
DEFINE DATA                                                               
*                                                                         
LOCAL USING MCIA0215   /* PROCESSAMENTO DE ERROS                          
LOCAL USING MCIL1837   /* RECUP. INF. TIPO ESTADO ALTERACAO A VALIDAR     
LOCAL USING MCIL3837   /* VALIDAR INF. TIPO ESTADO ALTERACAO A VALIDAR
LOCAL USING MCIL5837   /* ATU/INC INF.TIPO ESTADO ALTERACAO A VALIDAR     
*                                                                         
LOCAL                                                                     
*                                                                         
* ----------------------------------------------------------------------  
* VARIAVEIS DE GUARDA                                                     
* ----------------------------------------------------------------------  
1 GDA-RET-CODE            (N09)                                           
1 GDA-MSG-RET-CODE        (A80)                                           
1 GDA-PRG-ERRO            (A08)                                           
1 GDA-SEQ-ERRO            (N03)                                           
1 GDA-SQLCODE             (N09)                                           
1 GDA-SQLERR              (A70)                                           
1 GDA-SEQ-ERRO-PRG        (N03)                                           
1 GDA-ACAO-USU            (A01)                                           
1 GDA-TS-SEL              (A26/130)                                       
1 #GDA-TRATEI             (A01)                                           
1 #COD                    (I02)                                           
*                                                                          
* ----------------------------------------------------------------------  
* VARIAVEIS DO MAPA MCIM837C - LISTA TIPOS DE COMPROVANTE                 
* ----------------------------------------------------------------------  
1 #INICIO                      (N03,0)                                    
1 #M837C-CD-TIP-EST-ALT        (I2)                                       
1 #M837C-CD-USU-TIP-EST        (A008)                                     
1 #M837C-NM-TIP-EST-ALT        (A030)                                     
1 #M837C-TS-TIP-EST-ALT        (A026)                                     
1 #M837B-MSG                   (A060)                                     
*                                                                         
* ----------------------------------------------------------------------  
* VARIAVEIS DO MAPA MCIM837D - ALTERAR/INCLUIR TIPO ESTADO ALT  VALIDAR   
* ----------------------------------------------------------------------  
1 #CONFIRMA                        (A027)                                 
1 #CV-CONFIRMA                     (C)                                    
1 #CV-OPCAO2                       (C)                                    
1 #M837D-CD-TIP-EST-ALT            (I2)                                   
1 #M837D-CV-NM-TIP-EST-ALT         (C)                                    
1 #M837D-CV-TX-DCR-EST-ALT         (C)                                    
1 #M837D-NM-TIP-EST-ALT            (A030)                                 
1 #M837D-CD-USU-TIP-EST            (A008)                                 
1 #M837D-TS-TIP-EST-ALT            (A026)                                 
1 #M837D-TX-DCR-EST-ALT            (A45)                                  
1 #OPCAO2                          (A001)                                 
*                                                                         
1 #M837R-NM-TIP-EST-ALT            (A030)                                 
1 #M837R-CD-USU-TIP-EST            (A008)                                 
1 #M837R-TS-TIP-EST-ALT            (A026)                                 
1 #M837R-TX-DCR-EST-ALT            (A45)                                  
*                                                                         
1 #M837E-CD-TIP-EST-ALT            (I2)                                   
1 #M837E-CV-NM-TIP-EST-ALT         (C)                                    
1 #M837E-CV-DCR-TIP-EST-ALT        (C)                                    
1 #M837E-DCR-TIP-EST-ALT           (A45)                                  
1 #M837E-NM-TIP-EST-ALT            (A30)                                  
* ----------------------------------------------------------------------  
* VARIAVEIS DE USO DO PROGRAMA                                            
* ----------------------------------------------------------------------  
1 WGDA-CURSOR             (I04)                                           
1 GDA-IN-MTV-RSA-ATI      (A03/130)                                       
1 GDA-IN-MTV-RSA-IM       (A03/130)                                       
1 #MSG                    (A77)                                           
1 #FLAG                   (L) INIT <FALSE>                                
1 IND-RECHAMADA           (A01)                                           
1 COD-VALIDO              (A01)                                           
1 EXISTE-MAIS             (A01) INIT <' '>                                
1 PRX-PAG                 (A01) INIT <' '>                                
1 FUC                     (I01)                                           
1 #TS-REGISTRO            (A26) INIT <' '>                                
1 #I                      (N03) INIT <0>                                  
1 #I1                     (N03) INIT <0>                                  
1 #P                      (N03) INIT <0>                                  
1 IDX                     (N03) INIT <0>                                  
1 #IDX                    (N03) INIT <0>                                  
1 IDX-TEL                 (N03) INIT <0>                                  
1 #QT-X                   (N02)                                           
1 #POSICAO-X              (N02)                                           
1 TS-AUX                  (A26) INIT <' '>                                
1 REDEFINE TS-AUX                                                         
  2 TS-ANO                (N04)                                           
  2 FILLER                (A01)                                           
  2 TS-MES                (N02)                                           
  2 FILLER-1              (A01)                                           
  2 TS-DIA                (N02)                                           
  2 FILLER-3              (A01)                                           
  2 TS-HORA               (A08)                                           
  2 FILLER-4              (A07)                                           
1 DATA-AUX                (A10) INIT <' '>                                
1 REDEFINE DATA-AUX                                                       
  2 DT-DIA                (N02)                                           
  2 BARRA-1               (A01)                                           
  2 DT-MES                (N02)                                           
  2 BARRA-2               (A01)                                           
  2 DT-ANO                (N04)                                           
*                                                                         
1 #ULT-CD-TIP-EST-ALT    (I2)                                             
1 #GDA-CD-TIP-EST-ALT    (I2)                                             
1 #GDA-ALTERA            (A1)                                             
*                                                                         
* AUXILIARES DA SUB ROTINA SBTIMEDT (TIMESTAMP)                           
* ---------------------------------------------                           
1 FUNCAO-TS           (A03) INIT <'F01'>                                  
1 AREA                (A1028)                                             
1 REDEFINE AREA                                                           
  2 TAM-AREA          (B04)                                               
1 FORMATO             (B02) INIT <H'0001'>                                
1 TAMANHO             (B02) INIT <H'001A'>                                
1 DATA-HORA           (A26)                                               
1 RET-CODE            (I04)                                               
1 #QTD-T              (I02)                                               
*                                                                         
END-DEFINE                                                                
* ----------------------------------------------------------------------  
*                        PROCESSAMENTO PRINCIPAL                          
* ----------------------------------------------------------------------  
SET KEY ALL                                                               
SET KEY PF5  = 'MCIP9999'                                                 
SET KEY PF17 = 'MCIP9999'                                                 
*                                                                         
SET CONTROL 'M23'                                                         
SET CONTROL 'M-2'                                                         
SET CONTROL 'L'                                                           
*                                                                         
FETCH RETURN 'NATPLAN' 'MCIP0000'                                         
*                                                                         
* ----------------------------------------------------------------------  
* MONTA TELA INICIAL                                                      
* ----------------------------------------------------------------------  
MOVE 1 TO #COD                                                            
CALLNAT 'MCINQTDE' #COD #QTD-T                                            
#INICIO := 1                                                              
REPEAT                                                                    
  INPUT WITH TEXT #MSG MARK WGDA-CURSOR USING MAP 'MCIM837C'              
*                                                                         
  DECIDE ON FIRST VALUE OF *PF-KEY                                        
    VALUE 'ENTR'                                                          
      PERFORM CRITICA-CODIGO                                              
    VALUE 'PF2', 'PF14'                                                   
*     MOVE 1 TO #COD                                                      
*     CALLNAT 'MCINQTDE' #COD #QTD-T                                      
      MOVE #QTD-T TO #M837C-CD-TIP-EST-ALT                                
      #M837E-CD-TIP-EST-ALT := #QTD-T                                     
      RESET #MSG #GDA-ALTERA                                              
      PERFORM TRATA-INCLUSAO                                              
*                                                                         
      MOVE 0 TO #M837C-CD-TIP-EST-ALT                                     
    VALUE 'PF3', 'PF15'                                                   
      ESCAPE ROUTINE                                                      
    VALUE 'PF4', 'PF16'                                                   
      MOVE 0 TO #COD                                                      
      CALLNAT 'MCINCDEA' #COD                                             
      MOVE #COD TO #M837C-CD-TIP-EST-ALT                                  
      PERFORM CRITICA-CODIGO                                              
    VALUE 'PF5', 'PF17'                                                   
      TERMINATE                                                           
    NONE VALUE                                                            
      #MSG := ' Tecla nao Suportada.'                                     
  END-DECIDE                                                              
END-REPEAT                                                                
*                                                                         
* ---------------------------------------------------------------         
DEFINE SUBROUTINE CRITICA-CODIGO                                          
* ---------------------------------------------------------------         
*                                                                         
IF #M837C-CD-TIP-EST-ALT EQUAL 0                                          
  MOVE ' Informe Cod tipo de estado da alteração ou <PF4>.'               
    TO #MSG                                                               
  ESCAPE ROUTINE                                                          
END-IF                                                                    
IF #M837C-CD-TIP-EST-ALT >  #QTD-T                                        
  COMPRESS ' Informe F2 para incluir O proximo codigo'                    
    INTO #MSG   /*LEAVING NO                                              
  RESET #M837C-CD-TIP-EST-ALT                                             
  ESCAPE ROUTINE                                                          
END-IF                                                                    
*                                                                         
MOVE ' ' TO #MSG                                                          
RESET IND-RECHAMADA                                                       
  #GDA-ALTERA                                                             
#GDA-CD-TIP-EST-ALT  := #M837C-CD-TIP-EST-ALT                             
#ULT-CD-TIP-EST-ALT  := 0                                                 
*                                                                         
COD-VALIDO := 'S'                                                         
PERFORM VALIDA-CODIGO                                                     
IF COD-VALIDO = 'S'                                                       
*                                                                         
  REPEAT UNTIL IND-RECHAMADA    = 'N'                                     
    PERFORM RECUPERA-DADOS                                                
  END-REPEAT                                                              
*                                                                         
  IF  #GDA-ALTERA NE '*'                                                  
    ADD 1 TO #ULT-CD-TIP-EST-ALT                                          
    MOVE #ULT-CD-TIP-EST-ALT  TO #M837D-CD-TIP-EST-ALT                    
    MOVE #ULT-CD-TIP-EST-ALT  TO #M837E-CD-TIP-EST-ALT                    
    PERFORM TRATA-INCLUSAO                                                
  END-IF                                                                  
END-IF                                                                    
* ----------------------------------------------------------------------  
END-SUBROUTINE                                                            
* ----------------------------------------------------------------------  
* ----------------------------------------------------------------------  
DEFINE SUBROUTINE VALIDA-CODIGO                                           
* ----------------------------------------------------------------------  
SET CONTROL 'P=C'                                                         
RESET MCIS1837-PARM(*)                                                    
*                                                                         
MCIS1837-SYS-CHAMADOR         := 'MCI'                                    
MCIS1837-PRG-CHAMADOR         := 'MCIN5837'                               
MCIS1837-FUNCAO               := 1                                        
MCIS1837-USUARIO              := *USER                                    
MCIS1837-CD-TIP-EST-ALT-E     := #M837C-CD-TIP-EST-ALT                    
*                                                                         
CALL 'MCIS1837' MCIS1837-PARM (*)                                         
*                                                                         
DECIDE ON FIRST VALUE OF MCIS1837-RET-CODE                                
  VALUE 0                                                                 
    IGNORE                                                                
*                                                                         
  NONE VALUE                                                              
    IF MCIS1837-SQLCODE EQ 100                                            
      COD-VALIDO := 'N'                                                   
      MOVE ' Codigo não existente.'                                       
        TO #MSG                                                           
    ELSE                                                                  
      MOVE 'N'                   TO IND-RECHAMADA                         
      GDA-PRG-ERRO               := MCIS1837-PRG-ERRO                     
      GDA-RET-CODE               := MCIS1837-RET-CODE                     
      GDA-MSG-RET-CODE           := MCIS1837-MSG-RET-CODE                 
      GDA-SEQ-ERRO               := MCIS1837-SEQ-ERRO                     
      GDA-SQLCODE                := MCIS1837-SQLCODE                      
      GDA-SQLERR                 := MCIS1837-SQLERR                       
      GDA-SEQ-ERRO-PRG           := 1                                     
      PERFORM EXIBIR-ERRO                                                 
    END-IF                                                                
END-DECIDE                                                                
* ----------------------------------------------------------------------  
END-SUBROUTINE                                                            
* ----------------------------------------------------------------------  
* ----------------------------------------------------------------------  
DEFINE SUBROUTINE RECUPERA-DADOS                                          
* ----------------------------------------------------------------------  
SET CONTROL 'P=C'                                                         
RESET MCIS1837-PARM(*)                                                    
*                                                                         
MCIS1837-SYS-CHAMADOR         := 'MCI'                                    
MCIS1837-PRG-CHAMADOR         := 'MCIN5837'                               
MCIS1837-FUNCAO               := 2                                        
MCIS1837-USUARIO              := *USER                                    
MCIS1837-CD-TIP-EST-ALT-E     := #ULT-CD-TIP-EST-ALT                      
*                                                                         
CALL 'MCIS1837' MCIS1837-PARM (*)                                         
*                                                                         
DECIDE ON FIRST VALUE OF MCIS1837-RET-CODE                                
  VALUE 0                                                                 
    PERFORM MONTA-RETORNO                                                 
    MOVE 'N'                   TO IND-RECHAMADA                           
*                                                                         
  VALUE 88888                                                             
    PERFORM MONTA-RETORNO                                                 
    MOVE 'S'                   TO IND-RECHAMADA                           
*                                                                         
  NONE VALUE                                                              
    IF MCIS1837-SQLCODE EQ 100                                            
      MOVE 'N'                   TO IND-RECHAMADA                         
    ELSE                                                                  
      MOVE 'N'                   TO IND-RECHAMADA                         
      GDA-PRG-ERRO               := MCIS1837-PRG-ERRO                     
      GDA-RET-CODE               := MCIS1837-RET-CODE                     
      GDA-MSG-RET-CODE           := MCIS1837-MSG-RET-CODE                 
      GDA-SEQ-ERRO               := MCIS1837-SEQ-ERRO                     
      GDA-SQLCODE                := MCIS1837-SQLCODE                      
      GDA-SQLERR                 := MCIS1837-SQLERR                       
      GDA-SEQ-ERRO-PRG           := 1                                     
      PERFORM EXIBIR-ERRO                                                 
    END-IF                                                                
END-DECIDE                                                                
* ----------------------------------------------------------------------  
END-SUBROUTINE                                                            
* ----------------------------------------------------------------------  
* ----------------------------------------------------------------------  
DEFINE SUBROUTINE MONTA-RETORNO                                           
* --------------------------------------------------------------------    
RESET #M837D-CD-TIP-EST-ALT                                               
  #M837D-NM-TIP-EST-ALT                                                   
  #M837D-CD-USU-TIP-EST                                                   
  #M837D-TS-TIP-EST-ALT                                                   
  #M837D-TX-DCR-EST-ALT                                                   
*                                                                         
MOVE 1 TO #IDX                                                            
REP1. REPEAT                                                              
  IF  #IDX  >  MCIS1837-QT-TIP-S                                          
    ESCAPE BOTTOM (REP1.)                                                 
  END-IF                                                                  
*                                                                         
  IF  MCIS1837-CD-TIP-EST-ALT (#IDX) = #GDA-CD-TIP-EST-ALT                
    MOVE  '*' TO  #GDA-ALTERA                                             
    #M837D-CD-TIP-EST-ALT   := MCIS1837-CD-TIP-EST-ALT(#IDX)              
    #M837D-NM-TIP-EST-ALT   := MCIS1837-NM-TIP-EST-ALT(#IDX)              
    #M837R-NM-TIP-EST-ALT   := MCIS1837-NM-TIP-EST-ALT(#IDX)              
    #M837D-CD-USU-TIP-EST   := MCIS1837-CD-USU-TIP-EST (#IDX)             
    #M837D-TS-TIP-EST-ALT   := MCIS1837-TS-TIP-EST-ALT (#IDX)             
    #M837D-TX-DCR-EST-ALT   := MCIS1837-TX-DCR-EST-ALT (#IDX)             
    #M837R-TX-DCR-EST-ALT   := MCIS1837-TX-DCR-EST-ALT (#IDX)             
    PERFORM TRATA-ALTERACAO                                               
  END-IF                                                                  
  #ULT-CD-TIP-EST-ALT := MCIS1837-CD-TIP-EST-ALT(#IDX)                    
  ADD 1 TO #IDX                                                           
END-REPEAT                                                                
*                                                                         
* ----------------------------------------------------------------------  
END-SUBROUTINE                                                            
* ----------------------------------------------------------------------  
* ----------------------------------------------------------------------  
DEFINE SUBROUTINE TRATA-ALTERACAO                                         
* ----------------------------------------------------------------------  
*                                                                         
#CV-CONFIRMA                  := (AD=D)                                   
#CV-OPCAO2                    := (AD=D)                                   
#M837D-CV-TX-DCR-EST-ALT      := (AD=D)                                   
#M837D-CV-NM-TIP-EST-ALT      := (AD=D)                                   
*                                                                         
RESET #GDA-TRATEI  #OPCAO2 #MSG                                           
*                                                                         
INPUT WITH TEXT #MSG MARK WGDA-CURSOR USING MAP 'MCIM837D'                
*                                                                         
REPEAT                                                                    
  IF  #M837D-NM-TIP-EST-ALT EQ #M837R-NM-TIP-EST-ALT AND                  
      #M837D-TX-DCR-EST-ALT EQ #M837R-TX-DCR-EST-ALT                      
    ESCAPE BOTTOM                                                         
  END-IF                                                                  
  PERFORM VALIDA-DADOS2                                                   
  #M837D-CV-TX-DCR-EST-ALT      := (AD=P)                                 
  #M837D-CV-NM-TIP-EST-ALT      := (AD=P)                                 
  #CONFIRMA              := 'Confirma Alteracao (S/N) ?'                  
  INPUT WITH TEXT #MSG MARK WGDA-CURSOR USING MAP 'MCIM837D'              
*                                                                         
  DECIDE ON FIRST VALUE OF *PF-KEY                                        
    VALUE 'ENTR'                                                          
      PERFORM TRATA-CONFIRMA                                              
      IF #OPCAO2 = 'S' OR  #OPCAO2 = 's'                                  
        #MSG := ' Alteração realizada com sucesso'                        
        RESET #M837D-NM-TIP-EST-ALT                                       
          #GDA-CD-TIP-EST-ALT                                             
          #M837C-CD-TIP-EST-ALT                                           
          #M837D-TX-DCR-EST-ALT                                           
          #M837R-NM-TIP-EST-ALT                                           
          #M837R-TX-DCR-EST-ALT                                           
          #OPCAO2 #CONFIRMA                                               
      END-IF                                                              
      ESCAPE BOTTOM                                                       
    VALUE 'PF3', 'PF15'                                                   
      ESCAPE ROUTINE                                                      
    VALUE 'PF5', 'PF17'                                                   
      TERMINATE                                                           
    NONE VALUE                                                            
      REINPUT FULL ' Tecla nao Suportada.'                                
  END-DECIDE                                                              
END-REPEAT                                                                
*                                                                         
* ----------------------------------------------------------------------  
END-SUBROUTINE                                                            
* ----------------------------------------------------------------------  
*                                                                         
* ----------------------------------------------------------------------  
DEFINE SUBROUTINE TRATA-INCLUSAO                                          
* ----------------------------------------------------------------------  
*                                                                         
#CV-CONFIRMA                  := (AD=P)                                   
#CV-OPCAO2                    := (AD=P)                                   
#M837E-CV-NM-TIP-EST-ALT      := (AD=D)                                   
#M837E-CV-DCR-TIP-EST-ALT     := (AD=D)                                   
*                                                                         
REPEAT                                                                    
  INPUT WITH TEXT #MSG MARK WGDA-CURSOR USING MAP 'MCIM837E'              
  IF *PF-KEY = 'PF3' OR = 'PF15'                                          
    RESET #MSG                                                            
    ESCAPE ROUTINE                                                        
  END-IF                                                                  
  IF *PF-KEY = 'PF5'OR = 'PF17'                                           
    TERMINATE                                                             
  END-IF                                                                  
  IF #M837E-NM-TIP-EST-ALT EQ ' '                                         
    REINPUT  'Informe NM-TIP-EST-ALT ' MARK *#M837E-NM-TIP-EST-ALT        
  END-IF                                                                  
  IF #M837E-DCR-TIP-EST-ALT EQ ' '                                        
    REINPUT 'Informe DCR-TIP-EST-ALT ' MARK *#M837E-DCR-TIP-EST-ALT       
  END-IF                                                                  
  #CV-CONFIRMA                  := (AD=D)                                 
  #CV-OPCAO2                    := (AD=D)                                 
  #M837E-CV-NM-TIP-EST-ALT      := (AD=P)                                 
  #M837E-CV-DCR-TIP-EST-ALT     := (AD=P)                                 
  #CONFIRMA  := 'Confirma Inclusao (S/N) ?'                               
  INPUT WITH TEXT #MSG MARK WGDA-CURSOR USING MAP 'MCIM837E'              
  DECIDE ON FIRST VALUE OF *PF-KEY                                        
    VALUE 'ENTR'                                                          
      PERFORM VALIDA-DADOS                                                
      PERFORM TRATA-CONFIRMA                                              
      IF #OPCAO2 = 'S' OR  #OPCAO2 = 's' AND                              
          MCIS5837-RET-CODE = 0                                           
        ADD 1                         TO  #QTD-T                          
        #CV-OPCAO2                    := (AD=P)                           
        #M837E-CV-NM-TIP-EST-ALT      := (AD=P)                           
        #M837E-CV-DCR-TIP-EST-ALT     := (AD=P)                           
        RESET #OPCAO2  #CONFIRMA                                          
        #MSG := ' Inclusao realizada com sucesso'                         
      END-IF                                                              
      RESET #M837E-NM-TIP-EST-ALT                                         
        #M837E-DCR-TIP-EST-ALT                                            
        #OPCAO2 #CONFIRMA                                                 
      ESCAPE ROUTINE                                                      
    VALUE 'PF3', 'PF15'                                                   
      ESCAPE ROUTINE                                                      
    VALUE 'PF5', 'PF17'                                                   
      TERMINATE                                                           
    NONE VALUE                                                            
      REINPUT FULL ' Tecla nao Suportada.'                                
  END-DECIDE                                                              
END-REPEAT                                                                
*                                                                         
* ----------------------------------------------------------------------  
END-SUBROUTINE                                                            
* ----------------------------------------------------------------------  
*                                                                         
* ----------------------------------------------------------------------  
DEFINE SUBROUTINE TRATA-CONFIRMA                                          
* ----------------------------------------------------------------------  
IF #OPCAO2 =  'S' OR #OPCAO2  = 'N' OR                                    
    #OPCAO2 =  's' OR #OPCAO2  = 'n'                                      
  IGNORE                                                                  
ELSE                                                                      
  REINPUT  'Informe S ou N.' MARK *#OPCAO2                                
END-IF                                                                    
IF #OPCAO2 = 'S' OR #OPCAO2 = 's'                                         
  PERFORM MANUTENCAO-DADOS                                                
  ESCAPE ROUTINE                                                          
END-IF                                                                    
* ----------------------------------------------------------------------  
END-SUBROUTINE                                                            
* ----------------------------------------------------------------------  
*                                                                         
* ----------------------------------------------------------------------  
DEFINE SUBROUTINE VALIDA-DADOS                                            
* ----------------------------------------------------------------------  
*                                                                         
SET CONTROL 'P=C'                                                         
*                                                                         
RESET MCIS3837-PARM(*)                                                    
*                                                                         
MCIS3837-SYS-CHAMADOR         := 'MCI'                                    
MCIS3837-PRG-CHAMADOR         := 'MCIN5837'                               
MCIS3837-FUNCAO               := 1                                        
MCIS3837-USUARIO              := *USER                                    
*                                                                         
MCIS3837-CD-TIP-EST-ALT      := #M837E-CD-TIP-EST-ALT                     
MCIS3837-NM-TIP-EST-ALT      := #M837E-NM-TIP-EST-ALT                     
MCIS3837-TX-DCR-EST-ALT      := #M837E-DCR-TIP-EST-ALT                    
MCIS3837-CD-USU-TIP-EST      := *USER                                     
* MCIS3837-TS-TIP-EST-ALT      := *TIMX                                   
*                                                                         
***                                                                       
CALL 'MCIS3837' MCIS3837-PARM(*)                                          
*                                                                         
DECIDE ON FIRST VALUE OF MCIS3837-RET-CODE                                
  VALUE 0                                                                 
    IGNORE                                                                
*                                                                         
  NONE VALUE                                                              
*                                                                         
    DECIDE ON FIRST VALUE OF MCIS3837-SEQ-ERRO                            
*       CAMPO: #M837D-CD-TIP-EST-ALT                                      
      VALUE 4                                                             
        REINPUT FULL MCIS3837-MSG-RET-CODE MARK *#M837D-NM-TIP-EST-ALT    
          ALARM                                                           
*       CAMPO: #M837D-NM-TIP-EST-ALT                                      
      VALUE 5                                                             
        REINPUT FULL MCIS3837-MSG-RET-CODE MARK *#M837D-NM-TIP-EST-ALT    
          ALARM                                                           
*       CAMPO #M837D-CD-USU-TIP-EST                                       
*     VALUE 6                                                             
*       REINPUT FULL MCIS3837-MSG-RET-CODE MARK *#M837D-CD-USU-TIP-EST    
*       ALARM                                                             
*       CAMPO #M837D-TX-DCR-EST-ALT                                       
*     VALUE 7                                                             
*       REINPUT FULL MCIS3837-MSG-RET-CODE MARK *#M837D-TX-DCR-EST-ALT    
*         ALARM                                                           
      NONE VALUE                                                          
        REINPUT FULL MCIS3837-MSG-RET-CODE                                
    END-DECIDE                                                            
*                                                                         
END-DECIDE                                                                
*                                                                         
* ----------------------------------------------------------------------  
END-SUBROUTINE                                                            
* ----------------------------------------------------------------------  
*                                                                         
* ----------------------------------------------------------------------  
DEFINE SUBROUTINE VALIDA-DADOS2                                           
* ----------------------------------------------------------------------  
*                                                                         
RESET MCIS3837-PARM(*)                                                    
*                                                                         
MCIS3837-SYS-CHAMADOR         := 'MCI'                                    
MCIS3837-PRG-CHAMADOR         := 'MCIN5837'                               
MCIS3837-FUNCAO               := 2                                        
MCIS3837-USUARIO              := *USER                                    
*                                                                         
MCIS3837-CD-TIP-EST-ALT      := #M837D-CD-TIP-EST-ALT                     
MCIS3837-NM-TIP-EST-ALT      := #M837D-NM-TIP-EST-ALT                     
MCIS3837-CD-USU-TIP-EST      := *USER                                     
MCIS3837-TS-TIP-EST-ALT      := #M837D-TS-TIP-EST-ALT                     
MCIS3837-TX-DCR-EST-ALT      := #M837D-TX-DCR-EST-ALT                     
*                                                                         
SET CONTROL 'P=C'                                                         
CALL 'MCIS3837' MCIS3837-PARM(*)                                          
*                                                                         
MOVE MCIS3837-MSG-RET-CODE TO GDA-MSG-RET-CODE                            
*                                                                         
DECIDE ON FIRST VALUE OF MCIS3837-RET-CODE                                
  VALUE 0                                                                 
    IGNORE                                                                
*                                                                         
  NONE VALUE                                                              
*                                                                         
    DECIDE ON FIRST VALUE OF MCIS3837-SEQ-ERRO                            
*       CAMPO: #M837D-CD-TIP-EST-ALT                                      
      VALUE 4                                                             
        REINPUT FULL MCIS3837-MSG-RET-CODE MARK *#M837D-CD-TIP-EST-ALT    
          ALARM                                                           
*       CAMPO: MCIS3837-TX-DCR-MTV-RSA                                    
      VALUE 5                                                             
        REINPUT FULL MCIS3837-MSG-RET-CODE MARK *#M837D-NM-TIP-EST-ALT    
          ALARM                                                           
*       CAMPO #M837D-TX-ABVD-MTV-RSA                                      
      VALUE 6                                                             
        REINPUT FULL MCIS3837-MSG-RET-CODE MARK *#M837D-CD-USU-TIP-EST    
      NONE VALUE                                                          
        REINPUT FULL MCIS3837-MSG-RET-CODE                                
    END-DECIDE                                                            
*                                                                         
END-DECIDE                                                                
*                                                                         
* ----------------------------------------------------------------------  
END-SUBROUTINE                                                            
* ----------------------------------------------------------------------  
*                                                                         
* ----------------------------------------------------------------------  
DEFINE SUBROUTINE MANUTENCAO-DADOS                                        
* ----------------------------------------------------------------------  
*                                                                         
RESET MCIS1837-PARM(*)                                                    
*                                                                         
RESET MCIS5837-ENTRADA                                                    
*                                                                         
MCIS5837-SYS-CHAMADOR         := 'MCI'                                    
MCIS5837-PRG-CHAMADOR         := 'MCIN5837'                               
*                                                                         
IF #GDA-ALTERA = '*'                                                      
  MCIS5837-FUNCAO               := 2 /* ALTERACAO                         
  MCIS5837-CD-TIP-EST-ALT      := #M837D-CD-TIP-EST-ALT                   
  MCIS5837-NM-TIP-EST-ALT      := #M837D-NM-TIP-EST-ALT                   
  MCIS5837-TX-DCR-EST-ALT      := #M837D-TX-DCR-EST-ALT                   
ELSE                                                                      
  MCIS5837-FUNCAO               := 1 /* INCLUSAO                          
  MCIS5837-CD-TIP-EST-ALT      := #M837E-CD-TIP-EST-ALT                   
  MCIS5837-NM-TIP-EST-ALT      := #M837E-NM-TIP-EST-ALT                   
  MCIS5837-TX-DCR-EST-ALT      := #M837E-DCR-TIP-EST-ALT                  
END-IF                                                                    
*                                                                         
*                                                                         
MCIS5837-USUARIO             := *USER                                     
MCIS5837-CD-USU-TIP-EST      := *USER                                     
*                                                                         
TAM-AREA := 1024                                                          
SET CONTROL 'P=S'                                                         
CALL 'SBTIMEDT' USING FUNCAO-TS AREA FORMATO TAMANHO DATA-HORA            
ASSIGN RET-CODE = RET('SBTIMEDT')                                         
*                                                                         
IF RET-CODE NOT EQUAL 0                                                   
  GDA-PRG-ERRO               := 'SBTIMEDT'                                
  GDA-RET-CODE               := RET-CODE                                  
  GDA-MSG-RET-CODE           := 'Erro no retorno SBTIMEDT'                
  GDA-SEQ-ERRO               := 0                                         
  GDA-SQLCODE                := 0                                         
  GDA-SQLERR                 := 0                                         
  GDA-SEQ-ERRO-PRG           := 2                                         
  PERFORM EXIBIR-ERRO                                                     
END-IF                                                                    
*                                                                         
MCIS5837-TS-TIP-EST-ALT      := DATA-HORA                                 
*                                                                         
SET CONTROL 'P=C'                                                         
*                                                                         
CALL 'MCIS5837' MCIS5837-PARM(*)                                          
*                                                                         
DECIDE ON FIRST VALUE OF MCIS5837-RET-CODE                                
  VALUE 0                                                                 
*   ESCAPE ROUTINE                                                        
    IGNORE                                                                
  VALUE 305                                                               
    #MSG := ' Tipo Estado Alteração a Validar ja cadastrada.'             
  VALUE 803                                                               
    #MSG := ' Tipo Estado Alteração a Validar ja cadastrada.'             
  VALUE 304                                                               
    #MSG := ' Tipo Estado Alteração a Validar ja cadastrada.'             
  NONE VALUE                                                              
    REINPUT FULL MCIS5837-MSG-RET-CODE                                    
END-DECIDE                                                                
*                                                                         
* ----------------------------------------------------------------------  
END-SUBROUTINE                                                            
* ----------------------------------------------------------------------  
*                                                                         
* ----------------------------------------------------------------------  
DEFINE SUBROUTINE EXIBIR-ERRO                                             
* ----------------------------------------------------------------------  
*                                                                         
MCIN0215-PRG-CHAMADOR          := 'MCIN5837'                              
MCIN0215-PRG-ERRO              :=  GDA-PRG-ERRO                           
MCIN0215-RET-CODE              :=  GDA-RET-CODE                           
MCIN0215-MSG-RET-CODE          :=  GDA-MSG-RET-CODE                       
MCIN0215-SEQ-ERRO              :=  GDA-SEQ-ERRO                           
MCIN0215-SQLCODE               :=  GDA-SQLCODE                            
MCIN0215-SQLERR                :=  GDA-SQLERR                             
MCIN0215-SEQ-ERRO-PRG          :=  GDA-SEQ-ERRO-PRG                       
*                                                                         
CALLNAT 'MCIN0215' MCIN0215-PARM                                          
*                                                                         
* ----------------------------------------------------------------------  
END-SUBROUTINE                                                            
* ----------------------------------------------------------------------  
*                                                                         
END                                                                       
*                                                                         